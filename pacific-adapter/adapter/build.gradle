import com.square.buildsrc.BuildPlugins
import com.square.buildsrc.Libs

apply plugin: BuildPlugins.androidLibrary
apply plugin: BuildPlugins.mavenPublishPlugin

android {
    compileSdkVersion Libs.AndroidSdk.compileSdkVersion
    buildToolsVersion Libs.AndroidSdk.buildToolsVersion
    defaultConfig {
        minSdkVersion Libs.AndroidSdk.minSdkVersion
        targetSdkVersion Libs.AndroidSdk.targetSdkVersion
        versionCode Libs.AndroidSdk.versionCode
        versionName Libs.AndroidSdk.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Libs.AndroidX.annotation
    implementation Libs.AndroidX.Collection.collection
    implementation Libs.AndroidX.Collection.ktx
    implementation Libs.AndroidX.Recyclerview.recyclerview
    implementation Libs.AndroidX.viewpager
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

mavenPublish {
    releaseSigningEnabled = true
    targets {
        uploadArchives {
            releaseRepositoryUrl = properties.getProperty("RELEASE_REPOSITORY_URL")
            snapshotRepositoryUrl = properties.getProperty("SNAPSHOT_REPOSITORY_URL")
            repositoryUsername = properties.getProperty("SONATYPE_NEXUS_USERNAME")
            repositoryPassword = properties.getProperty("SONATYPE_NEXUS_PASSWORD")
        }

        installArchives {
            releaseRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
            snapshotRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
        }
    }
}